
TopLevel
    = << Stmt@+ #block>>;

Stmt
    = IfStmt EOS?
    / WhileStmt EOS?
    / ForStmt EOS?
    / ReturnStmt EOS?
    / ImportStmt EOS?
    / BreakStmt EOS?
    / ContinueStmt EOS?
    / DefineFunction EOS?
    / DefineClass EOS?
    / Print EOS?
    / With EOS?
    / Expr EOS?
    / EOS+
    / catch .*
    ;

EOS
    = ";"
    / NL
    / "#" (!'\n' .)* '\n'
    ;

With
    = <<"with" WS+ Expr@ WS+ "as" WS+ Symbol@ WS* ":" NL+ Block@ #with>>
    ;

Print
    = <<"print" WS+ Expr@ #print >>
    ;

IfStmt
    = <<"if" WS+ Expr@ WS* ":" Block@ (indent _* "else" WS* ":" NL+ Block@)? #if>>
    ;

WhileStmt
    = <<"while" WS+ Expr@ WS* ":" Block@ #while>>
    ;

ForStmt
    = <<"for" WS+ Symbol@ WS+ "in" WS+ Expr@ ":" Block@ #for>>
    ;

ReturnStmt
    = <<"return" WS+ Expr@ #return>>
    ;

ImportStmt
    = <<"import" WS* ImportSymbol@ (WS* "," WS* Symbol@)* #import>>
    / <<"from" WS* Symbol@ WS* "import" WS* ImportSymbol@ (WS* "," WS* ImportSymbol@)* #fromImport>>
    ;

DefineFunction
    = <<"def" WS+ Symbol@ "(" Params@ ")" WS* ":" NL+ Block@ #function>>
    ;

DefineClass
    = <<"class" WS+ Symbol@ (WS* "(" (Extend@)? WS* ")" )? WS* ":" NL+ ClassBlock@ #class>>
    ;

Extend
    = << Symbol@ #extend >>
    ;

BreakStmt
    = <<"break" #break>>
    ;

ContinueStmt
    = <<"continue" #continue>>
    ;

ReturnStmt
    = <<"return" WS+ Expr@ #return>>
    ;

Block
    = NL+ << (indent WS* Stmt@ NL?)+ #block>>
    / WS* << Stmt@ #block>>
    ;

ClassBlock
    = << (indent WS* ClassStmt@ NL?)+ #block>>;

ClassStmt
    = DefineFunction
    / Assignment
    ;

Expr
    = Assign
    ;

Assign
    = OrExpr <<@ WS* AssignOp WS* OrExpr@>>*
    ;

AssignOp
    = '=' !'=' #assign
    / '+='     #addeq
    / '-='     #subeq
    / '*='     #muleq
    / '/='     #diveq
    ;

OrExpr
    = AndExpr <<@ WS* 'or' WS* AndExpr@ #or >>*
    ;

AndExpr
    = BitwiseExpr <<@ WS* 'and' WS* BitwiseExpr@ #and >>*
    ;

BitwiseExpr
    = Equal <<@ WS* ('|' !'|' #bitor / '&' !'&' #bitand / '^' #bitxor) WS* Equal@ >>*
    ;

Equal
    = Relation <<@ WS*  EqOp@ WS* Relation@>>*
    ;

EqOp
    = 'is' WS+ 'not' #noteq
    / 'is' #eq
    / '==' #eq
    / '!=' #noteq
    ;

Relation
    = Shift <<@ _ ('<' !'<' #lt / '<=' #lte / '>' !'>' #gt / '>=' #gte) _ Shift@ >>*
    ;

Shift
    = Sum <<@ _ ('<<' #lshift / '>>' #rshift) _ Sum@ >>*
    ;

Sum
    = Product <<@ WS* ('+' #add / '-' #sub) WS* Product@ >>*
    ;

Product
    = Unary <<@ WS* ('*' #mul / '/' #div / '%' #mod) WS* Unary@ >>*
    ;

Unary
    = << ('!' #not / '-' #minus / '+' #plus / '~' #compl) Unary@ >>
    / Value <<@ Selector>>*
    ;

Selector
    = '.' Symbol@ #field
    / '[' WS* Expr@ WS* ']' #index
    / '(' << _* ( Expr@ ( _* ',' _* Expr@ )* )? #args >>@ ')'  #apply
    ;

Value
    = Symbol
    / StringLiteral
    / Number
    / ArrayLiteral
    / MapLiteral
    / TupleLiteral
    / << '(' Expr@ ')' #group >>
    ;

Assignment
    = Expr;

ArrayLiteral
    = << "[" _* Expr@? (_* "," _* Expr@)* _* "]" #array>>
    / << "[" _* Expr@ _* "for" _* Symbol@ _* "in" _* Expr@ (_* "if" _* Expr@)? _* "]" #listComprehension >>
    ;

TupleLiteral
    = << '(' Expr@ ')' #group >>
    / << "(" _* Expr@ (_* "," _* Expr@)+ _* ")" #array>>
    ;

MapLiteral
    = << "{" _* MapContent@? (_* "," _* MapContent@ )* _* "}" #map>>;

MapContent
    = << (StringLiteral@ / Symbol@) _* ":" _* Expr@ #keyvalue>>;

StringLiteral
    = '"' << ('\\"' / !'"' .)* #string >> '"' 
    / "'" << ("\\'" / !"'" .)* #string >> "'" 
    ;

Params
    = << Symbol@? (_* "," _* Symbol@)* _* #params>>;

Args
    = << Expr@? (_* "," _* Expr@)* _* #args>>;

Op
    = '+' #add / '-' #sub / '*' #mul / '/' #div / '%' #mod / '<=' #lte / '<' #lt / '>=' #gte / '>' #gt;

Number
    = << ('-' WS* / '+' WS*)?('0'/[1-9][0-9]*) '.' [0-9]+ #float >>
    / << ('-' WS* / '+' WS*)?('0'/[1-9][0-9]*) #integer>>
    ;

Symbol
    = << "True" #true >>
    / << "False" #false >>
    / << "None" #null >>
    / << [a-zA-Z_][a-zA-Z0-9_]* #name>>
    ;

Asterisk
    = << '*' #asterisk>>
    ;

ImportSymbol
    = Symbol@
    / Asterisk@
    ;

WS
    = [ \t];

NL
    = [\n];

_
    = WS
    / NL
    ;
